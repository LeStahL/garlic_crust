// Pest grammar for the plantuml class diagram language subset we're using
WHITESPACE = _{" " | "\t" | "\n" | "\r"}
COMMENT = _{"'" ~ (!"\n" ~ ANY)*}

string = { "\"" ~ (raw_string | escape)* ~ "\"" }
	raw_string = { (!("\\" | "\"") ~ ANY)+ }
	escape = { "\\" ~ (predefined | byte | unicode) }
		predefined = { "n" | "r" | "t" | "\\" | "0" | "\"" | "'" }
		byte = { "x" ~ hex{2} }
		unicode = { "u" ~ "{" ~ unicode_hex ~ "}" }
			unicode_hex = { hex{1, 6} }
		hex = _{ '0'..'9' | 'a'..'f' | 'A'..'F' }

quoted_identifier = {"\"" ~ identifier ~ "\""}
	identifier = @{(ASCII_ALPHA | "_")+ ~ (ASCII_ALPHANUMERIC | "_")*}

float = @{ (int ~ ("." ~ ASCII_DIGIT*)? ~ (^"e" ~ int)?) | (("+" | "-")? ~ ("." ~ ASCII_DIGIT*)? ~ (^"e" ~ int)?) }
    int = { ("+" | "-")? ~ ASCII_DIGIT+ }

file = {"@startuml" ~ identifier? ~ block* ~ "@enduml"}
	block = {node | edge}
		node = {"class" ~ identifier ~ "<" ~ identifier ~ ">" ~ "{" ~ attribute* ~ "}"}
			attribute = {"-" ~ identifier ~ ":" ~ attribute_value}
				attribute_value = {string | enum_entry | float}
					enum_entry = {(identifier ~ "::")* ~ identifier}
		edge = {identifier ~ quoted_identifier ~ "*--" ~ quoted_identifier ~ identifier}
