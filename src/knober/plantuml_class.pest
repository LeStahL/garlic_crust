// Pest grammar for the plantuml class diagram language subset we're using
WHITESPACE = _{" " | "\t" | "\n" | "\r"}

quoted_identifier = {"\"" ~ identifier ~ "\""}
	identifier = @{(ASCII_ALPHA | "_")+ ~ (ASCII_ALPHANUMERIC | "_")*}

float = @{ (int ~ ("." ~ ASCII_DIGIT*)? ~ (^"e" ~ int)?) | (("+" | "-")? ~ ("." ~ ASCII_DIGIT*)? ~ (^"e" ~ int)?) }
    int = { ("+" | "-")? ~ ASCII_DIGIT+ }

file_path = {windows_path | linux_path}
	windows_path = {file_name | ((ASCII_ALPHA{1} ~ ":\\")? ~ (file_name ~ "\\")+ ~ file_name?)}
		file_name = {(ASCII_ALPHANUMERIC | "-" | "_" | ".")+}
	linux_path = {file_name | ("/"? ~ (file_name ~ "/")* ~ file_name)}

file = {"@startuml" ~ identifier ~ block* ~ "@enduml"}
	block = {node | edge}
		node = {"class" ~ identifier ~ "<" ~ identifier ~ ">" ~ "{" ~ attribute* ~ "}"}
			attribute = {"-" ~ identifier ~ ":" ~ attribute_value}
				// attribute values can be floats, filename arguments or enum entries
				attribute_value = {quoted_file_name | enum_entry | float}
					quoted_file_name = {"\"" ~ file_path ~ "\""}
					enum_entry = {(identifier ~ "::")* ~ identifier}
		edge = {identifier ~ quoted_identifier ~ "*--" ~ quoted_identifier ~ identifier}
